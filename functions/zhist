# vim:ft=zsh

__zhist_usage () {

  cat <<- EOF

  Usage: zhist [OPTIONS] [SEARCH STRING]

    -d, --detail       Show return codes and durations
    -l, --limit n      Only show n rows. defaults to $LINES or 25

    -e, --exact        Don't match substrings

    -H, --host         Print the host column and show all hosts (otherwise current host)
    -H, --host x       Find entries from host x

    -f, --from x       Only show commands after date x
    -u, --until x      Only show commands before date x
    -s, --session n    Only show session n

    -i, --in           Find only entries run in the current dir or below
    -i, --in x         Find only entries in directory x or below
    -a, --at           Like --in, but excluding subdirectories

    -D, --debug        Debug output query that will be run
    -h, --help         Show this help text.

    -v, --vertical     Vertical Output

    --forget          Forget everything which matches in the history

EOF

}

zhist () {

  ARGA=("$@");

  local arg selcols horcols vercols cols where limit mode cmd forget exact hostwhere dirwhere debug count timecol until from query count_query;

  local sep=$'\x1f'

  timecol="strftime('%Y-%m-%d %H:%M', start_time, 'unixepoch', 'localtime') AS \"Date/Time\""
  selcols="id as ID, ${timecol}, session AS Session, user as User, dir as Directory"
  horcols="history.id as id, session, substr(places.dir,-50,50) as dir, users.user, substr(replace(commands.argv,X'0A','\n'),1,200) as argv, start_time"
  vercols="history.id as id, session, places.dir as dir, users.user,commands.argv as argv, start_time"
  colcount=6
  where="1=1"
  mode="horizontal"

  if [[ -p /dev/stdout ]]; then
    limit=""
  else
    limit="${$((LINES - 6)):-25}"
  fi

  for ((x=1;x<=$#ARGA;x++)); do
    case "${ARGA[x]}" in
      "-h"|"--help")
        __zhist_usage;
        return;
        ;;
      "-H"|"--host")
        i=1
        while [[ "${ARGA[++x]}" != '-'*  && -n "${ARGA[x]}" ]]; do
          if (( i > 1 )); then hostwhere+=", "; fi
          hostwhere="${hostwhere}\"${ARGA[x]}\""
          ((i++));
        done
        ((x--));

        if [[ -n "$hostwhere" && "$hostwhere" != '"all"' ]]; then
          where="${where} and places.host in (${hostwhere})"
        else
          hostwhere="all"
        fi
        horcols="${horcols}, places.host AS Hostname"
        vercols="${vercols}, places.host AS Hostname"
        selcols="${selcols}, Hostname"
        ((colcount++))
        ;;
      "-i"|"--in")
        while [[ "${ARGA[++x]}" != '-'*  && -n "${ARGA[x]}" ]]; do
          arg="${(S)ARGA[x]//'/''}"
          dirwhere="${dirwhere}${dirwhere:+ or }places.dir LIKE '${arg}%'"
        done
        where="${where}${dirwhere:+ and (${dirwhere})}"
        ;;
      "-a"|"--at")
        while [[ "${ARGA[++x]}" != '-'*  && -n "${ARGA[x]}" ]]; do
          arg="${(S)ARGA[x]//'/''}"
          dirwhere="${dirwhere}${dirwhere:+ or }places.dir = '${arg}'"
        done
        ((x--));
        where="${where}${dirwhere:+ and (${dirwhere})}"
        ;;
      "-f"|"--from")
        if [[ "${ARGA[++x]}" != '-'* && -n ${ARGA[x]} ]]; then
          from=${ARGA[x]};
        else
          ((x--));
          echo "No or bad value for from";
        fi
        case $from in
          "today") from="$(date "+%s" --date="$(date "+%F")")" ;;
          "yesterday") from="$(($(date "+%s" --date="$(date "+%F")") - 86400 ))" ;;
          *) from="$(date "+%s" --date="${from}")" ;;
        esac
        where="${where} AND start_time >= $from"
        ;;
      "-u"|"--until")
        if [[ "${ARGA[++x]}" != '-'* && -n ${ARGA[x]} ]]; then
          until=${ARGA[x]};
        else
          ((x--));
          echo "No or bad value for until";
        fi
        case $until in
          "today") until="$(date "+%s" --date="$(date "+%F")")" ;;
          "yesterday") until="$(($(date "+%s" --date="$(date "+%F")") - 86400 ))" ;;
          *) until="$(date "+%s" --date="${until}")" ;;
        esac
        where="${where} AND start_time <= $until"
        ;;
      "-D"|"--debug")
        debug=1
        ;;
      "-s"|"--session")
        if [[ "${ARGA[++x]}" != '-'* && -n ${ARGA[x]} ]]; then
          where="${where} AND session=\"${ARGA[x]}\""
        else
          ((x--));
          echo "No or bad value for session";
        fi
        ;;
      "-d"|"--detail")
        horcols="${horcols}, exit_status, duration "
        vercols="${vercols}, exit_status, duration "
        selcols="${selcols}, exit_status AS \"Return Code\",duration AS Duration "
        ((colcount+=2))
        ;;
      "--forget")
        forget=1
        ;;
      "-e"|"--exact")
        exact=1
        ;;
      "-l"|"--limit")
        if [[ "${ARGA[++x]}" == [0-9]* ]]; then
          limit=${ARGA[x]};
        else
          ((x--));
          echo "No or bad value for limit";
        fi
        ;;
      "-v"|"--vertical")
          mode="vertical";
        ;;
      *)
        cmd="${ARGA[x]}"
        ;;
    esac
  done

  selcols="${selcols}, argv as Command"

  if [[ -z ${hostwhere} ]]; then
    where="${where} AND places.host= \"$HOST\""
  fi

  if [[ -n "$cmd" ]]; then
    cmd="${(S)cmd//'/''}"
    if [[ -z $exact ]]; then
      where="${where} AND commands.argv glob '*${cmd}*'"
    else
      where="${where} AND commands.argv = '${cmd}'"
    fi
  fi

  if [[ -n $forget ]]; then
    limit=""
  fi

  if [[ -n "$limit" && "$mode" == "vertical" ]]; then
    ((limit=limit/(colcount+1)))
  fi

  if [[ "$mode" == "vertical" ]]; then
    cols="${vercols}"
  else 
    cols="${horcols}"
  fi
    query=$(<<- EOT
  SELECT
  ${selcols}
  FROM
  (
    SELECT
    ${cols}
    FROM history
    LEFT JOIN commands ON history.command_id = commands.id
    LEFT JOIN places ON history.place_id = places.id
    LEFT JOIN users ON history.user_id = users.id
    WHERE ${where}
    ORDER BY start_time DESC
    ${limit:+limit $limit}
  )
  ORDER BY start_time ASC;
EOT
);

  if [[ -n $debug ]]; then

    echo "$query";

  else

    count_query=$(<<- EOT
  SELECT COUNT(*)
  FROM history
  LEFT JOIN commands ON history.command_id = commands.id
  LEFT JOIN places ON history.place_id = places.id
  LEFT JOIN users ON history.user_id = users.id
  where ${where};
EOT
);

    count=$(__zhist_query "$count_query" | tail -n +2)

    case "$mode" in
      "horizontal")
        __zhist_query "${mode}" "$query" | column -t -s $sep | less -SXF
        ;;
      "vertical")
        __zhist_query "${mode}" "$query" | less -SXF
        ;;
    esac

    if [[ $limit == [0-9]* && $limit -lt $count ]]; then
      echo "(showing $limit of $count results)";
    fi

  fi

  if [[ -n $forget ]]; then

    read -rq "REPLY?Forget all these results? [y/n] "

    if [[ $REPLY =~ [yY] ]]; then

      __zhist_query <<- EOF
    DELETE FROM history
    WHERE history.id IN (
      SELECT history.id
      FROM history
      LEFT JOIN commands ON history.command_id = commands.id
      LEFT JOIN places ON history.place_id = places.id
      LEFT JOIN users ON history.user_id = users.id
      WHERE ${where}
    );
    DELETE FROM commands
    WHERE commands.id NOT IN (
      SELECT DISTINCT history.command_id FROM history
    );
EOF

    fi
  fi

}

zhist "$@"
