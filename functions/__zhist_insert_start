# vim:ft=zsh

__zhist_insert_start() {

  local cmd watch_fd pipe_fd;

  cmd="${1}"

  __zhist_watcher_check

  exec {watch_fd}<> "${__ZHIST_WATCH_FILE}"
  echo >&"${watch_fd}"  &> /dev/null
  exec {watch_fd}>&-

  for boring in "${ZHIST_IGNORE_COMMANDS[@]}"; do
    if [[ "$cmd" =~ $boring ]]; then
      return
    fi
  done

  __ZHIST_RAN_CMD=1

  exec {pipe_fd}<> "${__ZHIST_PIPE}"

  cmd="${(S)cmd//'/''}"

  >&"$pipe_fd" >>! "$ZHIST_QUERY_LOG" <<- EOF
  INSERT INTO commands (argv)
  VALUES ('${cmd}');
  INSERT INTO places (host, dir)
  VALUES ('$HOST', '${PWD}');
  INSERT INTO users (user)
  VALUES ('${USER}');
  INSERT INTO history (session, command_id, place_id, user_id, start_time)
  SELECT
    '${__ZHIST_SESSION}', commands.id, places.id, users.id, ${EPOCHSECONDS}
    FROM commands, places, users
    WHERE commands.argv = '${cmd}'
    AND places.host = '$HOST'
    AND users.user = '${USER}'
    AND places.dir = '${PWD}';
EOF

  exec {pipe_fd}>&-

}

__zhist_insert_start "$@"
